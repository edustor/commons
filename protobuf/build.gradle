group 'ru.edustor.commons'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.0.5-2'
    ext.spring_version = '4.3.4.RELEASE'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.1.0'
    }
}

compileKotlin {
    dependsOn('generateProto')
}


sourceSets {
    main {
        java {
            srcDirs += "${protobuf.generatedFilesBaseDir}/main/java"
        }
    }
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.google.protobuf:protobuf-java:3.1.0'
    compile "org.springframework:spring-context:$spring_version"
    compile "org.springframework:spring-web:$spring_version"
    compile 'com.googlecode.protobuf-java-format:protobuf-java-format:1.4'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}
